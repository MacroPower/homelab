---
kind: RoleBinding
apiVersion: rbac.authorization.k8s.io/v1
metadata:
  name: grafana-authentik-ns-secrets
roleRef:
  apiGroup: rbac.authorization.k8s.io
  kind: Role
  name: authentik-ns-secrets
subjects:
  - kind: ServiceAccount
    name: grafana-authentik-ns-secrets
    namespace: grafana

---
apiVersion: tf.upbound.io/v1beta1
kind: Workspace
metadata:
  name: authentik-grafana
spec:
  providerConfigRef:
    name: authentik
  writeConnectionSecretToRef:
    namespace: authentik
    name: authentik-grafana
  forProvider:
    source: Inline
    varFiles:
      - source: SecretKey
        format: HCL
        secretKeyRef:
          namespace: authentik
          name: authentik-cluster-credentials
          key: authentik.tfvars
    module: |
      variable "authentik_cluster_external_host" {
        type = string
      }

      variable "authentik_cluster_external_domain" {
        type = string
      }

      locals {
        domain_name = "grafana.${var.authentik_cluster_external_domain}"
      }

      data "authentik_flow" "default_authentication" {
        slug = "default-authentication-flow"
      }

      data "authentik_flow" "default_authorization" {
        slug = "default-provider-authorization-explicit-consent"
      }

      data "authentik_certificate_key_pair" "cert" {
        name = "Authentik Certificate"
      }

      data "authentik_scope_mapping" "scopes" {
        managed_list = [
          "goauthentik.io/providers/oauth2/scope-email",
          "goauthentik.io/providers/oauth2/scope-openid",
          "goauthentik.io/providers/oauth2/scope-profile",
        ]
      }

      data "authentik_user" "akadmin" {
        username = "akadmin"
      }

      resource "random_password" "grafana_client_id" {
        length  = 40
        lower   = true
        numeric = true
        upper   = false
        special = false
      }

      resource "random_password" "grafana_client_secret" {
        length  = 128
        lower   = true
        numeric = true
        upper   = true
        special = true
      }

      resource "authentik_provider_oauth2" "grafana_auth_provider" {
        name          = "grafana"
        client_id     = random_password.grafana_client_id.result
        client_secret = random_password.grafana_client_secret.result
        redirect_uris = [
          "https://${local.domain_name}/login/generic_oauth",
        ]
        property_mappings   = data.authentik_scope_mapping.scopes.ids
        authentication_flow = data.authentik_flow.default_authentication.id
        authorization_flow  = data.authentik_flow.default_authorization.id
        signing_key         = data.authentik_certificate_key_pair.cert.id
      }

      resource "authentik_application" "grafana" {
        name              = local.domain_name
        slug              = "grafana"
        protocol_provider = authentik_provider_oauth2.grafana_auth_provider.id
      }

      resource "authentik_group" "grafana_admins" {
        name   = "Grafana Admins"
        users  = [data.authentik_user.akadmin.id]
      }

      resource "authentik_group" "grafana_editors" {
        name   = "Grafana Editors"
        parent = authentik_group.grafana_admins.id
      }

      output "grafana_client_id" {
        value     = random_password.grafana_client_id.result
        sensitive = true
      }

      output "grafana_client_secret" {
        value     = random_password.grafana_client_secret.result
        sensitive = true
      }
