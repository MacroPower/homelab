---
apiVersion: tf.upbound.io/v1beta1
kind: Workspace
metadata:
  name: authentik-grafana
spec:
  providerConfigRef:
    name: authentik
  writeConnectionSecretToRef:
    namespace: grafana
    name: authentik-grafana
  forProvider:
    source: Inline
    varFiles:
      - source: SecretKey
        format: HCL
        secretKeyRef:
          namespace: authentik
          name: authentik-cluster-credentials
          key: authentik.tfvars
    module: |
      variable "authentik_cluster_cookie_domain" {
        type = string
      }

      variable "authentik_cluster_external_host" {
        type = string
      }

      variable "authentik_cluster_external_domain" {
        type = string
      }

      locals {
        domain_name = "grafana.${var.authentik_cluster_external_domain}"
      }

      data "authentik_flow" "default-authorization-flow" {
        slug = "default-provider-authorization-explicit-consent"
      }

      data "authentik_scope_mapping" "scopes" {
        managed_list = [
          "goauthentik.io/providers/oauth2/scope-email",
          "goauthentik.io/providers/oauth2/scope-openid",
          "goauthentik.io/providers/oauth2/scope-profile",
        ]
      }

      data "authentik_user" "akadmin" {
        username = "akadmin"
      }

      resource "random_password" "grafana-client-id" {
        length  = 40
        lower   = true
        numeric = true
        upper   = false
        special = false
      }

      resource "random_password" "grafana-client-secret" {
        length  = 128
        lower   = true
        numeric = true
        upper   = true
        special = true
      }

      resource "authentik_provider_oauth2" "grafana-auth-provider" {
        name          = "grafana"
        client_id     = random_password.grafana-client-id.result
        client_secret = random_password.grafana-client-secret.result
        redirect_uris = [
          "https://${local.domain_name}/login/generic_oauth",
        ]
        property_mappings  = data.authentik_scope_mapping.scopes.ids
        authorization_flow = data.authentik_flow.default-authorization-flow.id
      }

      resource "authentik_application" "grafana-application" {
        name              = local.domain_name
        slug              = "grafana"
        protocol_provider = authentik_provider_oauth2.grafana-auth-provider.id
      }

      resource "authentik_group" "grafana-admins" {
        name   = "Grafana Admins"
        users  = [data.authentik_user.akadmin.id]
      }

      resource "authentik_group" "grafana-editors" {
        name   = "Grafana Editors"
        parent = authentik_group.grafana-admins.id
      }

      output "grafana_client_id" {
        value     = random_password.grafana-client-id.result
        sensitive = true
      }

      output "grafana_client_secret" {
        value     = random_password.grafana-client-secret.result
        sensitive = true
      }
